plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

def integrationTest = sourceSets.create("integrationTest")


dependencies {
	implementation gradleApi()
	implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: '1.33.1'

    integrationTestImplementation gradleTestKit()
    integrationTestImplementation 'junit:junit:4.13.2'
    integrationTestImplementation 'commons-io:commons-io:2.11.0'
    integrationTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:2.1.20"
}

gradlePlugin {
    website = 'https://github.com/GoogleCloudPlatform/artifact-registry-maven-tools'
    vcsUrl = 'https://github.com/GoogleCloudPlatform/artifact-registry-maven-tools.git'
    plugins {
        artifactRegistryPlugin {
            id = 'com.google.cloud.artifactregistry.gradle-plugin'
            displayName = 'Artifact Registry Gradle Plugin'
            description = 'A Gradle plugin used to connect to Artifact Registry Maven repositories.'
            tags.set(['artifactregistry', 'gcp'])
            implementationClass = 'com.google.cloud.artifactregistry.gradle.plugin.ArtifactRegistryGradlePlugin'
        }
    }
}

tasks.register('integrationTest', Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnit()
}



publishing {
    repositories {
        maven {
            name = "testRepo"
            url = uri("file://${rootProject.buildDir}/repo")
        }
    }
}

